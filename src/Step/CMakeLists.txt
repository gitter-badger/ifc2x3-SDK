PROJECT(Step)

# The CMake version number.
SET(Step_VERSION_MAJOR 1)
SET(Step_VERSION_MINOR 2)
SET(Step_VERSION_PATCH 0)
SET(Step_VERSION "${Step_VERSION_MAJOR}.${Step_VERSION_MINOR}")
SET(Step_VERSION_FULL "${Step_VERSION}.${Step_VERSION_PATCH}")
SET(Step_SOVERSION 2)

FILE(GLOB PROJECT_SOURCES *.cpp)

FILE(GLOB PROJECT_INCLUDES ../../include/Step/*.h)
LIST(APPEND PROJECT_INCLUDES ${CMAKE_CURRENT_BINARY_DIR}/Step/StepExport.h)
LIST(APPEND PROJECT_INCLUDES ${CMAKE_CURRENT_BINARY_DIR}/Step/Config.h)

include(GenerateExportHeader)
ADD_COMPILER_EXPORT_FLAGS()

include_directories(BEFORE ${CMAKE_CURRENT_BINARY_DIR})

ADD_LIBRARY(Step ${SHARED_OR_STATIC} ${PROJECT_SOURCES} ${PROJECT_INCLUDES})

if(STEP_THREAD_SAFE)
    target_link_libraries(Step debug ${osg_LIBRARY_DEBUG} optimized ${osg_LIBRARY_RELEASE} ${OPENTHREADS_LIBRARIES})
endif()

if(USE_LOG4CPLUS)
    target_link_libraries(Step ${LOG4CPLUS_LIBRARIES})
endif()

GENERATE_EXPORT_HEADER( Step EXPORT_FILE_NAME Step/StepExport.h STATIC_DEFINE IFC2X3_STATIC)

configure_file(../../include/Step/Config.h.in Step/Config.h)

SET_TARGET_PROPERTIES(Step PROPERTIES PROJECT_LABEL "Library Step" VERSION ${Step_VERSION_FULL} SOVERSION ${Step_SOVERSION})

INSTALL(FILES ${PROJECT_INCLUDES} DESTINATION include/Step)

INSTALL(TARGETS Step
            RUNTIME DESTINATION bin
            LIBRARY DESTINATION lib${LIB_POSTFIX}
            ARCHIVE DESTINATION lib${LIB_POSTFIX}
)

if(WIN32)
    get_property(TARGET_NAME_DEBUG TARGET Step PROPERTY LOCATION_DEBUG)
    string(REGEX REPLACE "dll$" "pdb" TARGET_PDB_FILE ${TARGET_NAME_DEBUG})
    install(FILES ${TARGET_PDB_FILE} DESTINATION bin CONFIGURATIONS Debug )
endif()
